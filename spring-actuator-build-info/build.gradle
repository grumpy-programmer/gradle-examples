plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'io.spring.dependency-management' version '1.0.13.RELEASE'
    id 'com.gorylenko.gradle-git-properties' version '2.4.1'
    id 'java'
}

group = 'io.grumpy-programmer'
version = System.getenv('VERSION') ?: 'unknown'
sourceCompatibility = JavaVersion.VERSION_17

ext {
    repository = 'https://github.com/grumpy-programmer/gradle-examples'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    compileOnly 'org.projectlombok:lombok'
    developmentOnly 'org.springframework.boot:spring-boot-devtools'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

gitProperties {
    keys = ['git.branch', 'git.commit.id', 'git.commit.time']
    customProperty('git.github.repository', project.repository)
    customProperty('git.github.release', "${project.repository}/releases/tag/${project.version}")
    customProperty('git.github.commit', { "${project.repository}/commit/${it.head().id}" })
}

springBoot {
    buildInfo()
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named("jar") {
    enabled = false // to not build spring-actuator-build-info-0.0.1-plain.jar (not executable jar)
}
